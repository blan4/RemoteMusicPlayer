apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount =
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    productFlavors {
        dev {
            minSdkVersion 23 // To reduce compilation time
        }
        prod {
            minSdkVersion 15
        }
    }

    defaultConfig {
        applicationId "org.seniorsigan.musicroom"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode gitCommitCount
        versionName "$gitCommitCount"
        archivesBaseName = "musicroom-$versionCode"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

ext {
    anko_version = '0.8.2'
    support_version = '23.1.1'
    nanohttpd_version = '2.2.0'
    eventbus_version = '3.0.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:appcompat-v7:$support_version"

    compile "org.nanohttpd:nanohttpd:$nanohttpd_version"
    compile "org.nanohttpd:nanohttpd-nanolets:$nanohttpd_version"
    compile "org.greenrobot:eventbus:$eventbus_version"

    compile "org.jetbrains.anko:anko-sdk15:$anko_version"
    compile "org.jetbrains.anko:anko-support-v4:$anko_version"
    compile "org.jetbrains.anko:anko-appcompat-v7:$anko_version"

    compile 'com.vk:androidsdk:1.6.2'
    compile 'com.android.support:design:23.1.1'
}
